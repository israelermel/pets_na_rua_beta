apply from: 'build-modularization.gradle'
apply from: 'build-dependencies.gradle'

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.3'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                buildToolsVersion buildTools
                compileSdkVersion compileSdk

                testOptions {
                    unitTests {
                        returnDefaultValues = true
                        includeAndroidResources = true
                        animationsDisabled = true
                    }
                }
                defaultConfig {
                    minSdkVersion minSdk
                    targetSdkVersion targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility javaVersion
                    targetCompatibility javaVersion
                }

                dataBinding { enabled = true }

                lintOptions {
                    abortOnError false
                }

                androidExtensions {
                    experimental = true
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
